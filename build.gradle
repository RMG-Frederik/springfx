import java.text.SimpleDateFormat

Date buildDateTime = new Date()

buildscript {
    repositories {
        mavenCentral()
        maven { url 'https://plugins.gradle.org/m2/' }
    }
    dependencies {
        classpath 'net.nemerosa:versioning:2.6.1'
        classpath 'gradle.plugin.nl.javadude.gradle.plugins:license-gradle-plugin:0.14.0'
    } 
}

apply plugin: 'net.nemerosa.versioning'

ext {
    builtBy = System.properties['user.name']
    buildRevision = versioning.info.commit
    buildDate = new SimpleDateFormat('yyyy-MM-dd').format(buildDateTime)
    buildTime = new SimpleDateFormat('HH:mm:ss.SSSZ').format(buildDateTime)
    createdBy = "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})".toString()
}

allprojects {
	task envriontmentInfo {
	    group 'help'
	    description 'Prints envriontment information'
	    doLast {
	    	println "========================================================="
	        println "JAVA_OPTS:             ${System.env.JAVA_OPTS ?: "[]"}"
	        println "JAVA_HOME:             ${System.env.JAVA_HOME}"
	        println "Java version:          ${System.properties['java.version']}"
	        println "Gradle version:        ${gradle.gradleVersion}"
	        println "Subprojects:           ${rootProject.childProjects.keySet()}"
	        println "========================================================="
	        println "Project Properties:"
	        rootProject.ext.properties.each {key, value ->
	        	println "${key} : ${value}"
	        }
	        println "========================================================="
	    }
	}	
}

subprojects { subproject ->
    apply plugin: 'java'

    buildscript {
        repositories {
            mavenCentral()
            maven { url 'https://plugins.gradle.org/m2/' }
        }
    }
    repositories {
    	mavenLocal()
        mavenCentral()
        jcenter()  
    } 

    apply from: rootProject.file('gradle/code-coverage.gradle')
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}